/*
 * This Groovy source file was generated by the Gradle 'init' task.
 */
package at.schrottner.gradle

import org.gradle.testkit.runner.BuildResult
import org.gradle.testkit.runner.GradleRunner
import org.junit.jupiter.api.BeforeEach
import org.junit.jupiter.api.Test
import org.junit.jupiter.api.io.TempDir
import static org.assertj.core.api.Assertions.assertThat;

class GitlabRepositoriesPluginFunctionalTests {
	private pluginClasspath = getClass().classLoader.findResource("plugin-classpath.txt")
			.readLines()
			.collect { it.replace('\\\\', '\\\\\\\\') } // escape backslashes in Windows paths
			.collect { "'$it'" }
			.join(", ")

	File projectDir
	File settingsGradle
	File buildGradle

	@BeforeEach
	void setup(@TempDir File projectDir) {
		this.projectDir = projectDir

		settingsGradle = new File(projectDir, "settings.gradle")
		buildGradle = new File(projectDir, "build.gradle")
	}

	@Test
	void "only used in settings"() {
		//given:
		settingsGradle << """
            $apply    
			gitLab {
				${generateToken('DeployToken', 'DeployToken')}
            }
        """
		buildGradle << """
			task gitLabTask {}
		"""

		//when:
		BuildResult result = runTest()

		//then:
		assertThat(result.output)
				.contains("BUILD SUCCESSFUL")
				.containsSubsequence(
						"added Job-Token: jobToken",
						"added Deploy-Token: token0",
						"added Deploy-Token: token1"
				)
	}

	@Test
	void "only used in project"() {
		//given:
		buildGradle << """
            $apply    
			gitLab {
				${generateToken('DeployToken', 'DeployToken')}
            }
        """

		//when:
		BuildResult result = runTest()

		//then:
		assertThat(result.output)
				.contains("BUILD SUCCESSFUL")
				.containsSubsequence(
						"added Job-Token: jobToken",
						"added Deploy-Token: token0",
						"added Deploy-Token: token1"
				)
	}

	@Test
	void "used in settings and project"() {
		//given:
		settingsGradle << """ 
            $apply
			gitLab {
				${generateToken('DeployToken', 'DeployToken', 'DeployToken')}
            }
        """

		buildGradle << """
            $apply
            gitLab {
				${generateToken('PrivateToken', 'PrivateToken')}
            }
        """
		//when:
		BuildResult result = runTest()

		//then:
		assertThat(result.output)
				.contains("BUILD SUCCESSFUL")
				.containsSubsequence(
						"added Job-Token: jobToken",
						"added Deploy-Token: token0",
						"added Deploy-Token: token1",
						"Settings evaluated",
						"added Job-Token: jobToken",
						"added Private-Token: token0",
						"added Private-Token: token1"
				)
	}

	def getApply() {
		""" 
            import at.schrottner.gradle.auths.*      
 			buildscript {
				dependencies {
					classpath files($pluginClasspath)
				}
			}
			apply plugin: at.schrottner.gradle.GitlabRepositoriesPlugin    
		"""
	}

	private BuildResult runTest() {
		def runner = GradleRunner.create()
		runner.forwardOutput()
		runner.withPluginClasspath()
		runner.withArguments("gitLabTask", "-i", "-s")
		runner.withProjectDir(projectDir)
		runner.build()
	}

	private String generateToken(String... tokenTypes) {
		def output = ""
		tokenTypes.eachWithIndex { it, index ->
			output += """
			token($it) {
				it.key = 'token$index'
				it.value = 'test'
			}
			"""
		}
		return output
	}
}
